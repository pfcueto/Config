//$tab Main
//============================================================================
// Proyecto: UCI
// Empresa : HREUS
// Modelo  : Transform
// Creación: 10/01/2019
// Autores : Izertis. Josep Ma Sarroche
//============================================================================

//$tab Periodo
//============================================================================
LET vG.Periodo=vG.BasePath&'Config\Includes\UCI\01.Extract\Base\Periodo.qvs';
$(Must_Include=$(vG.Periodo))

//$tab Labels
//============================================================================
D_Encounter:
LOAD encounterId			                         as Uci_encounterId, 
     patientId				                         as encounter_patientId, 
     episodeId				                         as encounter_episodeId, 
     lifeTimeNumber			                         as Uci_encounter_HC, 
     Capitalize(lastName)&','&Capitalize(firstName)    as encounter_Name, 
     gender					                    as encounter_gender, 
     age					                         as encounter_age, 
     patientType			                         as encounter_patientType
FROM
$(vG.ExtractPath)D_Encounter.qvd(qvd);

D_Intervention:
LOAD interventionId			                         as Uci_interventionId, 
     shortLabel				                    as Uci_intervention
FROM
$(vG.ExtractPath)D_Intervention.qvd(qvd);

D_Attribute:
LOAD attributeId			                         as Uci_attributeId, 
     longLabel				                         as Uci_attribute
FROM
$(vG.ExtractPath)D_Attribute.qvd(qvd);

D_CareProvider:
LOAD careProviderId, 
     lastName&','&firstName                            as careProvider_name, 
      professionalDomain 	                         as careProvider_professionalDomain 
FROM
$(vG.ExtractPath)D_CareProvider.qvd(qvd);

D_ClinicalUnit:
LOAD clinicalUnitId			                         as Uci_clinicalUnitId, 
     displayLabel 			                         as Uci_clinicalUnit 
FROM
$(vG.ExtractPath)D_ClinicalUnit.qvd(qvd);

D_Bed:
LOAD bedId, 
     bedLabel
FROM
$(vG.ExtractPath)D_Bed.qvd(qvd);

//$tab PtCensus
// Necessitem Data Ingres - Data Alta per assignar assessment dins del ingrés que pertany
//============================================================================
PtCensus:
LOAD ptCensusId														as Uci_ptCensusId,
	 clinicalUnitId													as Uci_clinicalUnitId,
	 encounterId													as Uci_encounterId,
	 date(inTime,'DD/MM/YYYY hh:mm')								as Uci_inTime, 
     date(outTime,'DD/MM/YYYY hh:mm')								as Uci_outTime
FROM
$(vG.ExtractPath)PtCensus.qvd(qvd)
where (clinicalUnitId='3')  and year(inTime)>=$(vYearMin);

//$tab PtTreatment
// Necessitem Tractaments TRRC
//============================================================================
PtTreatment:
LOAD 
	 ptTreatmentId				as Uci_ptTreatmentId, 
     interventionId				as Uci_interventionId, 
     attributeId				as Uci_attributeId, 
     encounterId				as Uci_encounterId, 
     dataFocusId, 
     chartTime, 
     utcChartTime, 
     siteId, 
     descriptorId, 
     ongoingId, 
     materialId, 
     clinicalUnitId				as Uci_clinicalUnitId, 
     careProviderId, 
     terseForm, 
     verboseForm, 
     valueString						as Uci_valueString, 
     valueDateTime						as Uci_valueDateTime, 
     utcValueDateTime, 
     num(valueNumber,'##,##')			as Uci_valueNumber, 
     unitOfMeasure, 
     baseValueNumber, 
     baseUOM, 
     valueInstanceId, 
     valueConcept, 
     lowerNormal, 
     upperNormal, 
     remark, 
     mainState, 
     actionState, 
     chartDayId, 
     chartTimeId						as Uci_chartTimeId, 
     caseId, 
     systemId, 
     cisOrderId, 
     storeTime, 
     date(storeTime,'DD/MM/YYYY hh:mm')									as Uci_storeTime_hhmm, 
     date(floor(storeTime),'DD/MM/YYYY')								as Uci_storeTime,
     date(floor(storeTime),'DD/MM/YYYY') 								as Cal_pk, 
     utcStoreTime, 
     bedId, 
     cisPtInterventionId, 
     partNumberId, 
     partNumber
FROM
$(vG.ExtractPath)PtTreatment.qvd(qvd)
where (clinicalUnitId='3' or clinicalUnitId='1') and year(storeTime)>=$(vYearMin);

UCI_Treatment3:
Load
	'2' as tmp2,
	Uci_ptCensusId,
	Uci_clinicalUnitId,
	Uci_encounterId, 
	Uci_inTime,
	Uci_outTime
resident PtCensus;
left join
Load
	Cal_pk,
	Uci_ptTreatmentId,
	Uci_attributeId,
	Uci_interventionId,
	Uci_clinicalUnitId,				// left join
	Uci_encounterId, 				// left join
	Uci_storeTime_hhmm,
	Uci_storeTime,
	Uci_chartTimeId,
	Uci_valueString,
	Uci_valueDateTime,
	Uci_valueNumber
resident PtTreatment;

drop table PtTreatment;
drop table PtCensus;

// Ens quedem els treatment del ingrés
UCI_Treatment2:
Load
	'1'as tmp1,
	*
resident UCI_Treatment3
where Uci_storeTime_hhmm>=Uci_inTime and Uci_storeTime_hhmm<=Uci_outTime;

drop table UCI_Treatment3;


// Treatment diario para cada ingreso
UCI_Treatment:
// TRRC terapia
Load
	Uci_ptCensusId&'-'&Uci_clinicalUnitId&'-'&Uci_encounterId&'-'&Uci_interventionId&'-'&Uci_storeTime 		as Uci_Treatment_pk,
	Uci_ptCensusId,
	Uci_clinicalUnitId,
	Uci_encounterId, 
	Only(Uci_encounterId)																as Uci_TRRC_encounter, 
	Uci_interventionId,
	Uci_storeTime,
	max(Uci_chartTimeId)																as Uci_TRRC_min,
	count(Uci_valueString)																as Uci_TRRC
resident UCI_Treatment2
where Uci_attributeId='42504' and Uci_interventionId='5075'
group by Uci_ptCensusId,Uci_clinicalUnitId,Uci_encounterId,Uci_interventionId,Uci_storeTime;
join
// TRRC Diàlisi (ml/h)
Load
	Uci_ptCensusId&'-'&Uci_clinicalUnitId&'-'&Uci_encounterId&'-'&Uci_interventionId&'-'&Uci_storeTime 		as Uci_Treatment_pk,
	Uci_ptCensusId,
	Uci_clinicalUnitId,
	Uci_encounterId, 
	Uci_interventionId,
	Uci_storeTime,
	avg(Uci_valueNumber)																as Uci_TRRC_Dialisi_mlh
resident UCI_Treatment2
where Uci_attributeId='43377' and Uci_interventionId='6199'
group by Uci_ptCensusId,Uci_clinicalUnitId,Uci_encounterId,Uci_interventionId,Uci_storeTime;


drop table UCI_Treatment2;

//$tab LT
//============================================================================
LT_Treatment:
Load distinct
	Uci_Treatment_pk,
	Uci_ptCensusId,
	Uci_encounterId, 
	Uci_clinicalUnitId,
	Uci_interventionId,
	Uci_storeTime																		as Cal_pk,
	Uci_storeTime																		as Uci_Data,
	'UCI_STORE'																		as Tipus
resident UCI_Treatment;


STORE LT_Treatment into $(vG.LoadPath)LT_Treatment.qvd;
DROP FIELD Uci_storeTime,Uci_ptCensusId,Uci_clinicalUnitId,Uci_encounterId,Uci_interventionId from UCI_Treatment;
STORE UCI_Treatment into $(vG.LoadPath)UCI_Treatment.qvd;

//$tab Calendar
//============================================================================
LET vG.Calendar=vG.BasePath&'Config\Includes\Uci\02.Transform\Base\Calendar.qvs';
$(Must_Include=$(vG.Calendar))
